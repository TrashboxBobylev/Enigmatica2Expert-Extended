#modloaded thaumcraft randomtweaker
#reloadable
#priority 3000

import crafttweaker.entity.IEntityDefinition;
import crafttweaker.item.IItemStack;
import native.java.util.regex.Pattern;
import thaumcraft.aspect.CTAspectStack;

static emojiMap as CTAspectStack[string] = {
  'ğŸ’¨': <aspect:aer>,
  'ğŸ‘½': <aspect:alienis>,
  'âš—ï¸': <aspect:alkimia>,
  'à¶': <aspect:amogus>,
  'ğŸ’§': <aspect:aqua>,
  'âœ¨': <aspect:auram>,
  'ğŸ—¡ï¸': <aspect:aversio>,
  'ğŸº': <aspect:bestia>,
  'â˜€ï¸': Aspect.caeles,
  'ğŸ§ ': <aspect:cognitio>,
  'âœŠ': <aspect:desiderium>,
  'ğŸ²': <aspect:draco>,
  'ğŸ’€': <aspect:exanimis>,
  'ğŸ’£': Aspect.exitium,
  'ğŸ”¨': <aspect:fabrico>,
  'â™’': Aspect.fluctus,
  'ğŸ§Š': <aspect:gelum>,
  'ğŸŒ±': <aspect:herba>,
  'ğŸ‘¨': <aspect:humanus>,
  'ğŸ”¥': <aspect:ignis>,
  'ğŸ™Œ': Aspect.imperium,
  'ğŸ§¨': Aspect.infernum,
  'ğŸ› ï¸': <aspect:instrumentum>,
  'ğŸ•¯ï¸': <aspect:lux>,
  'âš™ï¸': <aspect:machina>,
  'ğŸ”·': <aspect:mana>,
  'ğŸ”©': <aspect:metallum>,
  'âš°ï¸': <aspect:mortuus>,
  'ğŸƒ': <aspect:motus>,
  'ğŸ¦„': <aspect:mythus>,
  'âŸ': <aspect:ordo>,
  'âš¡': <aspect:perditio>,
  'ğŸ”„': <aspect:permutatio>,
  'ğŸ’ª': <aspect:potentia>,
  'ğŸ”®': <aspect:praecantatio>,
  'ğŸ›¡ï¸': <aspect:praemunio>,
  'ğŸ€': <aspect:rattus>,
  'ğŸ©¸': <aspect:sanguis>,
  'ğŸ¦‰': <aspect:sensus>,
  'ğŸ›ï¸': Aspect.sonus,
  'ğŸ‘»': <aspect:spiritus>,
  'ğŸŒ‘': <aspect:tenebrae>,
  'â›°ï¸': <aspect:terra>,
  'ã€‡': <aspect:vacuos>,
  'ğŸƒ': Aspect.ventus,
  'â¤ï¸': <aspect:victus>,
  'ğŸ”—': <aspect:vinculum>,
  'ğŸ‘ï¸': Aspect.visum,
  'ğŸ‡': <aspect:vitium>,
  'ğŸ’': <aspect:vitreus>,
  'ğŸ•Šï¸': <aspect:volatus>,
};

function set(aspectString as string, item as IItemStack) as void {
  if (isNull(item)) return;
  if (aspectString.length() <= 0) {
    logger.logWarning('Empty aspect list for: ' ~ item.commandString);
    return;
  }
  val aspects = toAspects(aspectString);
  if (aspects.length <= 0) {
    logger.logWarning('Empty aspect list for: ' ~ item.commandString ~ ' aspects: ' ~ aspectString);
    return;
  }
  item.setAspects(aspects);
}

function setEntity(aspectString as string, entity as IEntityDefinition) as void {
  if (isNull(entity)) return;
  if (aspectString.length() <= 0) {
    logger.logWarning('Empty aspect list for entity: ' ~ entity.id);
    return;
  }
  val aspects = toAspects(aspectString);
  if (aspects.length <= 0) {
    logger.logWarning('Empty aspect list for entity: ' ~ entity.id ~ ' aspects: ' ~ aspectString);
    return;
  }
  entity.setAspects(aspects);
  <minecraft:spawn_egg>.withTag({EntityTag: {id: entity.id}}).setAspects(aspects);
}

static numberPattern as Pattern = Pattern.compile('^\\d+');

function toAspects(aspects as string) as CTAspectStack[] {
  val aStacks as string[] = aspects.trim().split('\\s+');
  if (aStacks.length <= 0) return [] as CTAspectStack[];
  val result = arrayOf(aStacks.length, null as CTAspectStack) as CTAspectStack[];
  for i, aStack in aStacks {
    if (aStack.length() < 1) {
      logger.logWarning(`Empty aspect string in: ${aspects}`);
      continue;
    }
    var aspect as CTAspectStack = null;
    val matcher = numberPattern.matcher(aStack);
    if (matcher.find()) {
      val numberStr = matcher.group(0) as string;
      val aspectAmount = numberStr as int;
      aspect = emojiMap[aStack.substring(numberStr.length())];
      if (!isNull(aspect))
        aspect = aspectAmount == 1 ? aspect : aspect * aspectAmount;
    }
    else {
      aspect = emojiMap[aStack];
    }
    if (isNull(aspect)) {
      logger.logWarning(`Aspect not found: ${aStack}`);
      continue;
    }
    result[i] = aspect;
  }
  return result;
}
